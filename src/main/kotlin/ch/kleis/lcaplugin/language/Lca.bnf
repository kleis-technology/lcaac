{
  parserClass="ch.kleis.lcaplugin.language.parser.LcaParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Lca"
  psiImplClassSuffix="Impl"
  psiPackage="ch.kleis.lcaplugin.psi"
  psiImplPackage="ch.kleis.lcaplugin.psi.impl"

  elementTypeHolderClass="ch.kleis.lcaplugin.psi.LcaTypes"
  elementTypeClass="ch.kleis.lcaplugin.psi.LcaElementType"
  tokenTypeClass="ch.kleis.lcaplugin.psi.LcaTokenType"

  mixin("input_exchange")="ch.kleis.lcaplugin.language.psi.mixin.InputExchangeMixin"
  implements("input_exchange")="ch.kleis.lcaplugin.language.psi.type.PsiExchangeElement"

  parserUtilClass="ch.kleis.lcaplugin.language.parser.ExtensionParser"

  tokens=[
      TYPE_KEYWORD= 'type'
      UNIT_KEYWORD = 'unit'
      PROCESS_KEYWORD= 'process'
      INPUTS_KEYWORD = 'inputs'
      PRODUCTS_KEYWORD = 'products'
      RESOURCES_KEYWORD = 'resources'
      EMISSIONS_KEYWORD = 'emissions'
      SUBSTANCE_KEYWORD = 'substance'
      REFERENCE_KEYWORD = 'reference'
      IMPACT_KEYWORD = 'impact'
      FACTORS_KEYWORD = 'factors'
      LAND_USE_KEYWORD = 'land_use'
      META_KEYWORD = 'meta'
      IDENTIFIER = "regexp:\w+"
      NUMBER = "regexp:\d+"
      LBRACE = 'left-bracket'
      SEPARATOR = 'separator'
      RBRACE = 'right-bracker'
      LTYPE = 'left-type'
      RTYPE = 'right-type'
      COMA = 'coma'
      LIST_ITEM = 'list'
      STRING = 'string'
      SPACE = "regexp:\s+"
      COMMENT_BLOCK_START = "/*"
      COMMENT_BLOCK_END = "*/"
      COMMENT_BLOCK_CONTENT = "comment-content"

  ]
}

lcaFile ::= (process | substance)*

// process

process ::= PROCESS_KEYWORD string_literal LBRACE process_body RBRACE {
  implements = ["com.intellij.psi.PsiNameIdentifierOwner"]
  mixin ="ch.kleis.lcaplugin.language.psi.mixin.ProcessDefinitionMixin"
}
process_body ::= (products | inputs | resources | emissions | land_use)* metadata?
inputs ::= INPUTS_KEYWORD LBRACE input_exchange* RBRACE {}
products ::= PRODUCTS_KEYWORD LBRACE product* RBRACE
resources ::= RESOURCES_KEYWORD LBRACE bio_exchange* RBRACE
factors ::= FACTORS_KEYWORD substance_id? SEPARATOR IDENTIFIER LBRACE factor* RBRACE
emissions ::= EMISSIONS_KEYWORD LBRACE bio_exchange* RBRACE
land_use ::= LAND_USE_KEYWORD LBRACE bio_exchange* RBRACE

// substance

substance ::= SUBSTANCE_KEYWORD substance_id LBRACE substance_body RBRACE {
    implements = [ "ch.kleis.lcaplugin.language.psi.type.Substance" ]
    elementTypeClass="ch.kleis.lcaplugin.language.psi.stub.SubstanceStubElementType"
    mixin = "ch.kleis.lcaplugin.language.psi.mixin.SubstanceMixin"
    stubClass = "ch.kleis.lcaplugin.language.psi.stub.SubstanceStub"
}
substance_body ::= substance_type unitType factors? metadata?
substance_type ::= TYPE_KEYWORD SEPARATOR (RESOURCES_KEYWORD | EMISSIONS_KEYWORD | LAND_USE_KEYWORD)
unitType ::= UNIT_KEYWORD SEPARATOR quantity

// exchanges

input_exchange ::= techno_exchange {
    mixin="ch.kleis.lcaplugin.language.psi.mixin.InputExchangeMixin"
    implements="ch.kleis.lcaplugin.language.psi.type.PsiExchangeElement"
}
product ::= techno_exchange {
    implements = [ "ch.kleis.lcaplugin.language.psi.type.Product" ]
    elementTypeClass="ch.kleis.lcaplugin.language.psi.stub.ProductStubElementType"
    mixin = "ch.kleis.lcaplugin.language.psi.mixin.ProductMixin"
    stubClass = "ch.kleis.lcaplugin.language.psi.stub.ProductStub"
}
meta bio_exchange ::= LIST_ITEM substance_id NUMBER quantity {
    recoverWhile=next_item
    mixin="ch.kleis.lcaplugin.language.psi.mixin.BioExchangeMixin"
    implements="ch.kleis.lcaplugin.language.psi.type.PsiExchangeElement"
}
private techno_exchange ::= LIST_ITEM string_literal NUMBER quantity {recoverWhile=next_item}

// factors

meta factor ::= LIST_ITEM string_literal IDENTIFIER? NUMBER {
    recoverWhile=next_item
}

// id substance

substance_id ::= string_literal (COMA string_literal (COMA string_literal)?)? {
    implements="com.intellij.psi.PsiNamedElement"
    mixin="ch.kleis.lcaplugin.language.psi.mixin.SubstanceIdMixin"
}

// meta-data

metadata ::= META_KEYWORD LBRACE property* RBRACE
property ::= LIST_ITEM IDENTIFIER SEPARATOR string_literal {recoverWhile=next_item}

// misc

string_literal ::= STRING {
    mixin="ch.kleis.lcaplugin.language.psi.mixin.StringLiteralMixin"
}
external quantity ::= parseQuantity
unit ::= IDENTIFIER {
     mixin= "ch.kleis.lcaplugin.language.psi.mixin.QuantityMixin"
     implements= "ch.kleis.lcaplugin.language.psi.type.PsiUnitElement"
}

private next_item ::= !(LIST_ITEM | RBRACE)
private next_process ::= !PROCESS_KEYWORD

