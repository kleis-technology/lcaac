{
  parserClass="com.github.albanseurat.lcaplugin.language.parser.LcaParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Lca"
  psiImplClassSuffix="Impl"
  psiPackage="com.github.albanseurat.lcaplugin.psi"
  psiImplPackage="com.github.albanseurat.lcaplugin.psi.impl"

  elementTypeHolderClass="com.github.albanseurat.lcaplugin.psi.LcaTypes"
  elementTypeClass="com.github.albanseurat.lcaplugin.psi.LcaElementType"
  tokenTypeClass="com.github.albanseurat.lcaplugin.psi.LcaTokenType"

  mixin("dataset_definition")="com.github.albanseurat.lcaplugin.language.psi.mixin.DatasetDefinitionMixin"
  implements("dataset_definition")="com.intellij.psi.PsiNameIdentifierOwner"

  mixin("input_exchange")="com.github.albanseurat.lcaplugin.language.psi.mixin.InputExchangeMixin"
  implements("input_exchange")="com.github.albanseurat.lcaplugin.language.psi.type.PsiExchangeElement"

  parserUtilClass="com.github.albanseurat.lcaplugin.language.parser.ExtensionParser"

  tokens=[
      TYPE_KEYWORD= 'type'
      UNIT_KEYWORD = 'unit'
      DATASET_KEYWORD= 'dataset'
      INPUTS_KEYWORD = 'inputs'
      PRODUCTS_KEYWORD = 'products'
      RESOURCES_KEYWORD = 'resources'
      EMISSIONS_KEYWORD = 'emissions'
      SUBSTANCE_KEYWORD = 'substances'
      LAND_USE_KEYWORD = 'land_use'
      META_KEYWORD = 'meta'
      IDENTIFIER = "regexp:\w+"
      NUMBER = "regexp:\d+"
      LBRACE = 'left-bracket'
      SEPARATOR = 'separator'
      RBRACE = 'right-bracker'
      LTYPE = 'left-type'
      RTYPE = 'right-type'
      COMA = 'coma'
      LIST_ITEM = 'list'
      STRING = 'string'
      SPACE = "regexp:\s+"
  ]
}

lcaFile ::= (dataset_definition | substance)*

// dataset

dataset_definition ::= DATASET_KEYWORD string_literal LBRACE dataset_body RBRACE
dataset_body ::= (products | inputs | resources | emissions | land_use)* metadata?
inputs ::= INPUTS_KEYWORD LBRACE input_exchange* RBRACE {}
products ::= PRODUCTS_KEYWORD LBRACE product* RBRACE
resources ::= RESOURCES_KEYWORD LBRACE bio_exchange* RBRACE
emissions ::= EMISSIONS_KEYWORD LBRACE bio_exchange* RBRACE
land_use ::= LAND_USE_KEYWORD LBRACE bio_exchange* RBRACE

// substance

substance ::= SUBSTANCE_KEYWORD substance_id LBRACE substance_body RBRACE {
    implements = [ "com.github.albanseurat.lcaplugin.language.psi.type.Substance" ]
    elementTypeClass="com.github.albanseurat.lcaplugin.language.psi.stub.SubstanceStubElementType"
    mixin = "com.github.albanseurat.lcaplugin.language.psi.mixin.SubstanceMixin"
    stubClass = "com.github.albanseurat.lcaplugin.language.psi.stub.SubstanceStub"
}
substance_body ::= substance_type unitType metadata?
substance_type ::= TYPE_KEYWORD SEPARATOR (RESOURCES_KEYWORD | EMISSIONS_KEYWORD | LAND_USE_KEYWORD)
unitType ::= UNIT_KEYWORD SEPARATOR quantity

// exchanges

input_exchange ::= techno_exchange {
    mixin="com.github.albanseurat.lcaplugin.language.psi.mixin.InputExchangeMixin"
    implements="com.github.albanseurat.lcaplugin.language.psi.type.PsiExchangeElement"
}
product ::= techno_exchange {
    implements = [ "com.github.albanseurat.lcaplugin.language.psi.type.Product" ]
    elementTypeClass="com.github.albanseurat.lcaplugin.language.psi.stub.ProductStubElementType"
    mixin = "com.github.albanseurat.lcaplugin.language.psi.mixin.ProductMixin"
    stubClass = "com.github.albanseurat.lcaplugin.language.psi.stub.ProductStub"
}
meta bio_exchange ::= LIST_ITEM substance_id NUMBER quantity {
    recoverWhile=next_item
    mixin="com.github.albanseurat.lcaplugin.language.psi.mixin.BioExchangeMixin"
    implements="com.github.albanseurat.lcaplugin.language.psi.type.PsiExchangeElement"
}
private techno_exchange ::= LIST_ITEM string_literal NUMBER quantity {recoverWhile=next_item}

// id substance

substance_id ::= string_literal (COMA string_literal (COMA string_literal)?)? {
    implements="com.intellij.psi.PsiNamedElement"
    mixin="com.github.albanseurat.lcaplugin.language.psi.mixin.SubstanceIdMixin"
}

// meta-data

metadata ::= META_KEYWORD LBRACE property* RBRACE
property ::= LIST_ITEM IDENTIFIER SEPARATOR string_literal {recoverWhile=next_item}

// misc

string_literal ::= STRING {
    mixin="com.github.albanseurat.lcaplugin.language.psi.mixin.StringLiteralMixin"
}
external quantity ::= parseQuantity
unit ::= IDENTIFIER {
     mixin= "com.github.albanseurat.lcaplugin.language.psi.mixin.QuantityMixin"
     implements= "com.github.albanseurat.lcaplugin.language.psi.type.PsiUnitElement"
}

private next_item ::= !(LIST_ITEM | RBRACE)
private next_dataset ::= !DATASET_KEYWORD

