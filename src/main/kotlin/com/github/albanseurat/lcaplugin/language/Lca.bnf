{
  parserClass="com.github.albanseurat.lcaplugin.language.parser.LcaParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Lca"
  psiImplClassSuffix="Impl"
  psiPackage="com.github.albanseurat.lcaplugin.psi"
  psiImplPackage="com.github.albanseurat.lcaplugin.psi.impl"

  elementTypeHolderClass="com.github.albanseurat.lcaplugin.psi.LcaTypes"
  elementTypeClass="com.github.albanseurat.lcaplugin.psi.LcaElementType"
  tokenTypeClass="com.github.albanseurat.lcaplugin.psi.LcaTokenType"

  mixin("dataset_definition")="com.github.albanseurat.lcaplugin.language.psi.mixin.DatasetDefinitionMixin"
  implements("dataset_definition")="com.intellij.psi.PsiNameIdentifierOwner"

  mixin("input_exchange")="com.github.albanseurat.lcaplugin.language.psi.mixin.InputExchangeMixin"
  implements("input_exchange")="com.github.albanseurat.lcaplugin.language.psi.PsiExchangeElement"

  mixin("product_exchange")="com.github.albanseurat.lcaplugin.language.psi.mixin.ProductExchangeMixin<com.github.albanseurat.lcaplugin.language.psi.stub.ProductStub>"
  implements("product_exchange")=["com.github.albanseurat.lcaplugin.language.psi.PsiProductElement"
    "com.intellij.psi.StubBasedPsiElement<com.github.albanseurat.lcaplugin.language.psi.stub.ProductStub>"]

  mixin("quantity")="com.github.albanseurat.lcaplugin.language.psi.mixin.QuantityMixin"
  implements("quantity")="com.github.albanseurat.lcaplugin.language.psi.PsiUnitElement"

  parserUtilClass="com.github.albanseurat.lcaplugin.language.parser.ExtensionParser"

  tokens=[
      DATASET_KEYWORD= 'dataset'
      INPUTS_KEYWORD = 'inputs'
      PRODUCTS_KEYWORD = 'products'
      RESOURCES_KEYWORD = 'resources'
      EMISSIONS_KEYWORD = 'emissions'
      SUBSTANCE_KEYWORD = 'substances'
      META_KEYWORD = 'meta'
      IDENTIFIER = "regexp:\w+"
      UNIT = "regexp:\w+"
      NUMBER = "regexp:\d+"
      LBRACE = 'left-bracket'
      SEPARATOR = 'separator'
      RBRACE = 'right-bracker'
      LIST_ITEM = 'list'
      STRING = 'string'
      SPACE = "regexp:\s+"
  ]
}

lcaFile ::= (dataset_definition | substance_definition)*

dataset_definition ::= DATASET_KEYWORD (IDENTIFIER | string_literal) LBRACE dataset_body RBRACE

substance_definition ::= SUBSTANCE_KEYWORD (IDENTIFIER | string_literal) LBRACE substance_body RBRACE

dataset_body ::= (products | inputs | resources | emissions)* metadata?

substance_body ::= metadata?

inputs ::= INPUTS_KEYWORD LBRACE input_exchange* RBRACE

products ::= PRODUCTS_KEYWORD LBRACE product_exchange* RBRACE

resources ::= RESOURCES_KEYWORD LBRACE bio_exchange* RBRACE

emissions ::= EMISSIONS_KEYWORD LBRACE bio_exchange* RBRACE

metadata ::= META_KEYWORD LBRACE property* RBRACE

meta input_exchange ::= exchange

meta product_exchange ::= exchange {
    implements = [ "com.github.albanseurat.lcaplugin.language.psi.PsiProductElement" ]
    elementTypeClass="com.github.albanseurat.lcaplugin.language.psi.stub.ProductStubElementType"
    mixin = "com.github.albanseurat.lcaplugin.language.psi.mixin.ProductExchangeMixin"
    stubClass = "com.github.albanseurat.lcaplugin.language.psi.stub.ProductStub"
}

meta bio_exchange ::= exchange

property ::= LIST_ITEM IDENTIFIER SEPARATOR string_literal {recoverWhile=next_exchange}

private exchange ::= LIST_ITEM (IDENTIFIER | string_literal) NUMBER <<parseQuantity>> {recoverWhile=next_exchange}

private next_exchange ::= !(LIST_ITEM | RBRACE)

private next_dataset ::= !DATASET_KEYWORD

private string_literal ::= STRING

quantity ::= UNIT

